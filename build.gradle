apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'application'

mainClassName = "hakd.Main"

group = 'com.github.rsgm'
description = 'Hacking game'
version = '0.0.3'
ext.gdxVersion = "1.0-SNAPSHOT";
targetCompatibility = 1.6

jar {
    manifest {
        attributes('Title': 'Hak\'d', 'Version': version)
    }
}

sourceSets {
    main {
        java.srcDirs 'src/main/java/'
        resources.srcDirs 'src/main/resources/', 'src/test/resources/'
    }
}


task packageHakd(dependsOn: "installApp", type: Zip) {
    description = 'Packages the install directory to a zip'
    archiveName = "Hakd-${version}.zip"
    into('Hakd/') {
        from 'build/install/Hakd/'

        into('bin') {
            from 'README.md'
            from 'LICENSE.md'
            into('docs/') {
                from 'docs/'
            }
        }

        into('bin/res/') { // put all files that gdx can't handle here
            into('python/') { from 'build/resources/main/python/' }
            into('textfiles/') { from 'build/resources/main/textfiles/' }
        }
    }
}



repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    compile "org.python:jython-standalone:2.7-b1"
}

