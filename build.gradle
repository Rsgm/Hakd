buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath 'com.palominolabs.gradle.task:gradle-git-clone-task:0.0.2'
    }
}

version = '0.0.4'

allprojects {
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    ext {
        sourceCompatibility = 1.7
        appName = 'Hakd'
        description = 'Hacking game'
        gdxVersion = '1.4.2-SNAPSHOT'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
}


project(":desktop") {
    apply plugin: 'java'
    apply plugin: 'application'

    dependencies {
        compile project(":core")

        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    }
}

project(":core") {
    apply plugin: 'java'

    /**
     * clone gdx-shaders to use as a dependency
     */
    task cloneGdxShaders(type: com.palominolabs.gradle.task.git.clone.GitCloneTask) {
        dir = file('../gdx-shaders')
        uri = 'https://github.com/Rsgm/gdx-shaders.git'
        treeish = 'v1.4' // a git hash, or tag name, or branch name
    }

    /**
     * clone libnoiseforjava to use as a dependency
     */
    task cloneLibNoise(type: com.palominolabs.gradle.task.git.clone.GitCloneTask) {
        dir = file('../libnoiseforjava')
        uri = 'https://github.com/Rsgm/libnoiseforjava.git'
        treeish = 'v1.0' // a git hash, or tag name, or branch name
    }

    dependencies {
        compile project(':gdx-shaders')
        compile project(':libnoiseforjava')

        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"


        //compile 'jline:jline:2.12'
        compile 'net.sf.jopt-simple:jopt-simple:4.7-beta-1'
        compile 'org.orbisgis:jdelaunay:0.5.3'// not sure if I want this, or to turn the backbones into a tree structure
        compile 'org.python:jython-standalone:2.7-b3'
    }
}

tasks.eclipse.doLast {
    delete ".project"
}
